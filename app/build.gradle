apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'

static def getTime() {
    return new Date().format("yyyyMMddHHmmss", TimeZone.getDefault())
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId APPLICATION_ID
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode VERSION_CODE as int
        versionName VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
//    resourcePrefix 'app_'
    }
    signingConfigs {
        keyConfig {
            keyAlias KEY_MOBILE_ALIAS
            keyPassword KEY_PASSWORD
            storeFile file(KEY_FILE)
            storePassword KEY_PASSWORD
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            debuggable false
            signingConfig signingConfigs.keyConfig
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'IS_DEBUG_MODE', IS_DEBUG_MODE_RELEASE
            buildConfigField 'String', 'TEST_API_URL', TEST_API_URL_RELEASE
        }
        debug {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            debuggable true
            signingConfig signingConfigs.keyConfig
            buildConfigField 'boolean', 'IS_DEBUG_MODE', IS_DEBUG_MODE_DEBUG
            buildConfigField 'String', 'TEST_API_URL', TEST_API_URL_DEBUG
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = getTime() + "_" + APP_OUTPUT_NAME + "_" + VERSION_NAME + "_" + variant.buildType.name + ".apk"
        }
    }

}
/* 处理 butterknife 在lib中的应用*/
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.jakewharton:butterknife-gradle-plugin:$rootProject.ext.butterknifeVersion"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    annotationProcessor "com.alibaba:arouter-compiler:$rootProject.ext.arouterCompileVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.butterknifeVersion"
    implementation project(':module_baselib')
    implementation project(':module_test')
}
